\subsection{Definición de una alerta}
\label{definir-alerta}

Una vez que se tiene instalado y configurado \gls{term:kapacitor}, ya es
posible definir alertas.

Para esto, es necesario crear un archivo utilizando el lenguage
\gls{term:tick_script}. A modo de ejemplo, crearemos un archivo de nombre
\texttt{cpu\_alert.tick} con la siguiente configuración:

\begin{lstlisting}

stream
    |from()
        .measurement('cpu')
    |alert()
        .crit(lambda: "usage_idle" <  70)
        .log('/tmp/alerts.log')

\end{lstlisting}

Una vez creado el archivo de configuración, es posible usarlo para definir una
alerta de tipo stream con el siguiente comando:

\begin{lstlisting}
kapacitor define cpu_alert -type stream -tick cpu_alert.tick -dbrp cadvisor.autogen
\end{lstlisting}

Con la configuración anterior cargada, \gls{term:kapacitor} ya tiene la
información necesaria para lanzar la alerta. Para que \gls{term:kapacitor}
comience a notificar la alerta, se la debe habilitar. Esta tarea puede
realizarse con el siguiente comando:

\begin{lstlisting}
kapacitor enable cpu_alert
\end{lstlisting}

Finalmente, para consultar si la alerta se encuentra cargada y visualizar los
datos que estén siendo procesados, se puede ejecutar el siguiente comando:

\begin{lstlisting}
kapacitor show cpu_alert
\end{lstlisting}

En nuestro caso, este comando nos retornará un mensaje similar al siguiente:

\begin{lstlisting}
ID: cpu_alert
ID: cpu_alert
Error: 
Template: 
Type: stream
Status: enabled
Executing: true
Created: 14 Mar 17 20:56 UTC
Modified: 14 Mar 17 21:22 UTC
LastEnabled: 14 Mar 17 21:21 UTC
Databases Retention Policies: ["cadvisor"."autogen"]
TICKscript:
stream
    |from()
        .measurement('cpu')
    |alert()
        .crit(lambda: "usage_idle" <  70)
        .log('/tmp/alerts.log')

DOT:
digraph cpu_alert {
graph [throughput="0.00 points/s"];

stream0 [avg_exec_time_ns="0" ];
stream0 -> from1 [processed="0"];

from1 [avg_exec_time_ns="0" ];
from1 -> alert2 [processed="0"];

alert2 [alerts_triggered="0" avg_exec_time_ns="0" crits_triggered="0" infos_triggered="0" oks_triggered="0" warns_triggered="0" ];
}
\end{lstlisting}

Para definir alertas útiles es importante analizar las situaciones ante las
cuáles podría ser importante ser avisado, y también cuál será el medio de 
comunicación a través del cual llegarán las mismas.

A continuación mostraremos un ejemplo sobre cómo puede ser usado
\gls{term:tick_script} para definir alertas más complejas.

\begin{lstlisting}
stream
    |from()
        .measurement('cpu')
    |eval(lambda: 100.0 - "usage_idle")
        .as('used')
    |groupBy('service', 'datacenter')
    |window()
        .period(1m)
        .every(1m)
    |percentile('used', 95.0)
    |eval(lambda: sigma("percentile"))
        .as('sigma')
        .keep('percentile', 'sigma')
    |alert()
        .id('{{ .Name }}/{{ index .Tags "service" }}/{{ index .Tags "datacenter"}}')
        .message('{{ .ID }} is {{ .Level }} cpu-95th:{{ index .Fields "percentile" }}')
        .warn(lambda: "sigma" > 2.5)
        .crit(lambda: "sigma" > 3.0)
        .log('/tmp/alerts.log')

        .post('https://alerthandler.example.com')

        .exec('/bin/custom_alert_handler.sh')

        .slack()
        .channel('#alerts')

        .pagerDuty()

        .victorOps()
        .routingKey('team_rocket')
\end{lstlisting}


El \eng{script} toma las medidas del porcentaje de uso de \gls{acro:cpu}, las
invierte para obtener el porcentaje de tiempo en que la \gls{acro:cpu} se
encuentra ociosa, las agrupa por servicio y centro de datos en ventanas de 1
minuto. Luego calcula el \gls{term:percentil} 95 y genera alertas en caso de
que se pasen determinados umbrales.

Finalmente se guardan los datos en un \eng{log}, se envían a un \eng{endpoint}
determinado, se ejecuta un script que maneje las alertas y se envía a Slack, a
PagerDuty y a VictorOps.

\gls{term:kapacitor} además, permite enviar datos a \gls{term:influx} de la
siguiente manera:

\begin{lstlisting}
 stream
        |eval(lambda: "errors" / "total")
            .as('error_percent')
        |influxDBOut()
            .database('mydb')
            .retentionPolicy('myrp')
            .measurement('errors')
            .tag('kapacitor', 'true')
            .tag('version', '0.2')
\end{lstlisting}


Esto puede resultar muy útil en caso de que queramos alimentar a nuestra
instancia de \gls{term:influx} con datos procesados por \gls{term:kapacitor}.
Incluso, se podría almacenar información de las alertas en \gls{term:influx}
para tener un seguimiento de las mismas (ver \autoref{del-sistema-de-alertas}).

En esta sección hemos mostrado cómo usar \gls{term:tick_script} para definir
alertas para que sean lanzadas por \gls{term:kapacitor}. A continuación
explicaremos algunos problemas que suelen estar presentes en los sistemas de
alertas y una forma en que los podemos solucionar.
